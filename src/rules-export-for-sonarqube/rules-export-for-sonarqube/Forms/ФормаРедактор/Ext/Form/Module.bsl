
Перем ОценкаПравил;
Перем СимволовВОкончанииHTMLОписания;

Процедура ПравилаКВыгрузкеПриАктивизацииСтроки(Элемент)
	
	Если ЭлементыФормы.ПравилаКВыгрузке.ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЭлементыФормы.РедакторHTML.УстановитьТекст(ЭлементыФормы.ПравилаКВыгрузке.ТекущиеДанные.Description);
	
КонецПроцедуры

// Формирование таблицы

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ПравилаКВыгрузке.Очистить();
	ВыборкаПравил = ВыборкаПравил();
	ПрочитатьОценкуПравил();
	
	Для Каждого Правило Из ВыборкаПравил.Строки Цикл
		
		Ошибка = ПравилаКВыгрузке.Добавить();
		Ошибка.Code = СокрЛП(Правило.ОшибкаКод);
		Ошибка.Name = Правило.ОшибкаНаименование;
		Ошибка.Active = Истина;
		Ошибка.NeedForCertificate = Правило.ТребованиеПрименяетсяДля1ССовместимо;
		УстановитьСвойстваТипаИКритичности(Ошибка, Правило);
		Ошибка.Description = ОписаниеОшибкиВHTML(Правило);
		
	КонецЦикла;
	
	Сообщить(ПравилаКВыгрузке.Количество());
	
КонецПроцедуры

Функция ВыборкаПравил()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТребованияККонфигурации.Ошибка КАК Ошибка,
		|	ТребованияККонфигурации.Требование КАК Требование,
		|	ТребованияККонфигурации.Требование.СсылкаНаСтандарт КАК ТребованиеСсылкаНаСтандарт,
		|	ТребованияККонфигурации.Ошибка.Код КАК ОшибкаКод,
		|	ТребованияККонфигурации.Ошибка.Наименование КАК ОшибкаНаименование,
		|	ТребованияККонфигурации.Ошибка.Критичность КАК ОшибкаКритичность,
		|	ТребованияККонфигурации.Ошибка.Рекомендация КАК ОшибкаРекомендация,
		|	ТребованияККонфигурации.Требование.Описание КАК ТребованиеОписание,
		|	ТребованияККонфигурации.Требование.ОписаниеHTML КАК ТребованиеОписаниеHTML,
		|	ТребованияККонфигурации.Требование.ПрименяетсяДля1ССовместимо КАК ТребованиеПрименяетсяДля1ССовместимо
		|ИЗ
		|	РегистрСведений.ТребованияККонфигурации КАК ТребованияККонфигурации
		|ГДЕ
		|	ТребованияККонфигурации.Конфигурация = &Конфигурация
		|	И ТребованияККонфигурации.Ошибка <> ЗНАЧЕНИЕ(Справочник.ОбнаруживаемыеОшибки.ПустаяСсылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОшибкаКод
		|ИТОГИ
		|	МАКСИМУМ(ОшибкаКод),
		|	МАКСИМУМ(ОшибкаНаименование),
		|	МАКСИМУМ(ОшибкаКритичность),
		|	МАКСИМУМ(ТребованиеПрименяетсяДля1ССовместимо)
		|ПО
		|	Ошибка"
	);
	Запрос.УстановитьПараметр("Конфигурация", КонфигурацияИсточникПравил);
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции

Процедура ПрочитатьОценкуПравил()
	
	МакетСПравилами = ЭтотОбъект.ПолучитьМакет("ПравилаОценка");
	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(МакетСПравилами.ПолучитьТекст());
	ОценкаПравил = ПрочитатьJSON(Чтение);
	
КонецПроцедуры

Процедура УстановитьСвойстваТипаИКритичности(ОписаниеПравила, Правило)
	
	РезультатПоискаВОценке = НайтиПравилоВОценке(Правило);
	
	Если РезультатПоискаВОценке <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(ОписаниеПравила, РезультатПоискаВОценке, "Type,Severity,effortMinutes");
		
	ИначеЕсли Правило.ОшибкаКритичность = Перечисления.УровниКритичностиОшибок.Совместимо Тогда
		
		ОписаниеПравила.Type = "BUG";
		ОписаниеПравила.Severity = "CRITICAL";
		
	ИначеЕсли Правило.ОшибкаКритичность = Перечисления.УровниКритичностиОшибок.Обязательно Тогда
		
		ОписаниеПравила.Type = "CODE_SMELL";
		ОписаниеПравила.Severity = "CRITICAL";
		
	Иначе
		
		ОписаниеПравила.Type = "CODE_SMELL";
		ОписаниеПравила.Severity = "INFO";
		
	КонецЕсли;
	
КонецПроцедуры

Функция НайтиПравилоВОценке(Правило)
	
	Для Каждого ОценкаПравила Из ОценкаПравил Цикл
		
		КодНачало = СокрЛП(Правило.ОшибкаКод) + " :";
		
		Если ОценкаПравила.ruleId = Правило.ОшибкаНаименование ИЛИ СтрНачинаетсяС(ОценкаПравила.ruleId, КодНачало) Тогда
			
			Возврат ОценкаПравила;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ОписаниеОшибкиВHTML(Правило)
	
	Если Правило.Строки.Количество() = 1 Тогда
		
		Возврат ВырезатьТело(Правило.Строки[0].ТребованиеОписаниеHTML);
		
	ИначеЕсли Правило.Строки.Количество() > 1 Тогда
		
		Описания = Новый Массив();
		
		Для Каждого Требование Из Правило.Строки Цикл
			
			Описания.Добавить(ВырезатьТело(Требование.ТребованиеОписаниеHTML));
			
		КонецЦикла;
		
		Возврат СтрСоединить(Описания, Символы.ПС + Символы.ПС + Символы.ПС);
		
	Иначе
		
		Возврат "Нет описания";
		
	КонецЕсли;
	
КонецФункции

Функция ВырезатьТело(Текст)
	
	ТекстВРЕГ = ВРег(Текст);
	
	ПозицияНачалаТела = СтрНайти(ТекстВРЕГ, "<BODY");
	ПозицияКонцаТела = СтрНайти(ТекстВРЕГ, "</BODY>");
	
	Если ПозицияНачалаТела > 0 И ПозицияКонцаТела > 0 Тогда
		
		Возврат СокрЛП(Сред(Текст, ПозицияНачалаТела + СтрДлина("<BODY>"), ПозицияКонцаТела - (ПозицияНачалаТела + СтрДлина("<BODY>"))));
		
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

// Запись в файл

Процедура ОсновныеДействияФормыВыгрузитьВJSON(Кнопка)
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.ОткрытьФайл(ПутьКФайлуВыгрузки);
	ЗаписатьJSON(ЗаписьJSON, ДанныеДляЗаписи());
	ЗаписьJSON.Закрыть();
	
КонецПроцедуры

Функция ДанныеДляЗаписи()
	
	Правила = Новый Массив();
	
	Для Каждого ПравилоКВыгрузке Из ПравилаКВыгрузке Цикл
		
		НовыйЭлемент = Новый Структура("Code,Type,Severity,Name,Description,Active,NeedForCertificate,EffortMinutes");
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ПравилоКВыгрузке);
		Правила.Добавить(НовыйЭлемент);
		
	КонецЦикла;
	
	Возврат Новый Структура("Rules", Правила);
	
КонецФункции

СимволовВОкончанииHTMLОписания = 13;

